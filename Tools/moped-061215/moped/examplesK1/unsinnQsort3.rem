// the quicksort part is irrelevant for the property to be checked

define N 3
define M 4 #(1<<N) - 2
define DEFAULT_INT_BITS N

int a[1,M];

init main;

error:	goto error;
ok:	goto ok;

module void qs(int left, int right)
{
	int eq, lo, hi, piv;

qs:
	//if
	//:: true -> skip;
	//:: true -> return;
	//fi;

	if
	:: (left >= right) -> return;
	:: else		   -> eq = left, lo = left, hi = right, piv = a[right];
	fi;

	do
	:: ((lo <= hi) && (a[hi] > piv))	-> hi = hi - 1;
	:: ((lo <= hi) && (a[hi] < piv))	-> eq = eq + 1,
						   lo = lo + 1,
						   a[eq] = a[hi],
						   a[lo] = a[eq],
						   a[hi] = a[lo];
	:: ((lo <= hi) && (a[hi] == piv))	-> lo = lo + 1,
						   a[lo] = a[hi],
						   a[hi] = a[lo];
	:: (lo == hi + 1) 			-> qs(left, eq-1);
						   break;
	od;

	qs(lo, right);

	return;
}

module void start()
{
	int bit(1);
	bit = 1;
	qs(1,M);
	if
	:: (bit == 1) 	-> goto ok;
	:: else		-> goto error;
	fi;
}
main: 	start();
	
